AzureDiagnostics
| where ResourceProvider == "MICROSOFT.ANALYSISSERVICES" and Resource contains "<yourservername>"
| where OperationName != "VertiPaqSEQueryEnd" or EventSubclass_s == "0"
//| where ServerName_s contains "-"
//calculate the amount of time spent in the engine
| extend Engine_CPUTime=extract(@"([^,]*)", 1,CPUTime_s, typeof(long))
//summarize the QPU and Duration metrics and grab the query text by each request (RootActivityId)
| summarize cpu = sum(Engine_CPUTime)/1000, TimeInSeconds = max(toint(Duration_s))/1000, max(iif(OperationName == "QueryBegin", TextData_s, "")) by RootActivityId_g
//order by the most expensive query
| order by cpu desc

//use this query to narrow in on a heavier qpu query from above. 
AzureDiagnostics
| where RootActivityId_g == "<yourraidhere>"
//| where OperationName == "QueryEnd"
| project OperationName, CPUTime_s, TextData_s, EventSubclass_s, CurrentTime_t, Duration_s
| order by CurrentTime_t asc

AzureDiagnostics
| where ResourceProvider == "MICROSOFT.ANALYSISSERVICES" and ServerName_s contains "<yourservername>"
| where OperationName != "VertiPaqSEQueryEnd" or EventSubclass_s == "0"
| where extract(@"([^,]*)", 1,Duration_s, typeof(long)) > 0
//| where ServerName_s contains "-"
| extend Engine_CPUTime=extract(@"([^,]*)", 1,CPUTime_s, typeof(long))
| summarize qpu = sum(Engine_CPUTime)/3000/4 by bin(StartTime_t, 1s)
| render timechart
